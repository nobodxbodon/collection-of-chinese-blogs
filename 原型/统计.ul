using * in 原型.通用
using * in 原型.md相关
using 取文件最初提交时间 in 原型.git功用

// 待做：提取与 列表 功能相同部分

统计数据 = []
for 仓库地址 in 仓库表 {
    目标目录 = 仓库表[仓库地址]
    格式 = 格式表[仓库地址]
    仓库本地路径 = os.path.join(源数据路径, 目标目录, 格式 == "自定义" ? 路径表[目标目录] : 路径表[格式])

    所占空间 = 取所占空间(os.path.join(源数据路径, 目标目录))

    所有简报 = 根据路径提取所有简报(仓库本地路径)
    文件个数 = #所有简报

    //println("`目标目录`: `所有简报`")
    
    更新时间 = ""
    最新博文 = ""
    for 简报 in 所有简报 {
        时间戳 =  取时间戳(简报) or 取文件最初提交时间(简报['仓库路径'], 简报['路径'])
        if 更新时间 < 时间戳 {
          更新时间 = 时间戳
          最新博文 = 取题目(简报)
        }

    }
    
    统计数据.append(tuple(目标目录, 文件个数, 所占空间, 更新时间, 最新博文))
    println("`目标目录` 无 md 文件！") if 文件个数 == 0
}

写入csv文件("原型/统计数据.csv", 统计数据)

总占用空间, 总博文数, 总博客数 = 0, 0, 0

for 数据 in 统计数据 {
  总占用空间 += 数据[2]
  总博客数 += 1
  总博文数 += 数据[1]
}

println("`总博客数` 个博客、`总博文数` 篇博文, 占用空间 `总占用空间/1000.0` GB")
