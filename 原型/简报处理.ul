using os

using * in 原型.md相关
using * in 原型.网页相关

func 取多种博文题目(简报) {
  if 简报.本地相对路径 == "cyril3.github.io" {
    return 取网页题目(简报)
  } else {
    return 取题目(简报)
  }
}

func 取各仓库简报() {
  各仓库简报 = {:}
  for 仓库地址 in 仓库表 {
    目标目录 = 仓库表[仓库地址]

    各仓库简报[仓库地址] = 取仓库内博文简报(仓库地址, 目标目录)
  }
  return  各仓库简报
}

func 取仓库内博文简报(仓库地址, 目标目录) {
  格式 = 格式表[仓库地址]
  仓库本地路径 = os.path.join(源数据路径, 目标目录, 格式 == "自定义" ? 路径表[目标目录] : 路径表[格式])
  所有简报 = 根据路径提取所有简报(仓库本地路径)
  for 简报 in 所有简报 {
    简报.本地相对路径 = 目标目录
    // print(简报.本地相对路径 + ' > ' + 简报.路径)
    简报.题目 = 取多种博文题目(简报)
    // println(' > ' + 简报.题目)
    简报.时间 = 取时间戳(简报)
  }
  return 所有简报
}

func 根据路径提取所有简报(仓库本地路径) {
  所有简报 = []
  for 当前路径, 目录名, 所有文件 in os.walk(仓库本地路径) {
    for 文件 in 所有文件 {
      文件路径 = os.path.join(当前路径, 文件)
      简报 = 简报结构()

      简报.仓库本地路径 = 仓库本地路径
      简报.文件名 = 文件
      简报.路径 = 文件路径

      if 简报.的格式 == 格式.md or 简报.的格式 == 格式.html{
        // println(简报.路径)
        简报 = 添加头部信息(文件路径, 简报)
      }
      // 待做：根据内容识别，如笔误的文件名：2018-01-14-java注解和反射.mc
      所有简报.append(简报) if 简报.的格式 != 格式.其他
    }
  }
  return 所有简报
}
